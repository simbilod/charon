
# The two-dimensional mesh will be defined on a surface of
# this brick
brick x 1 y 0.5 z 0.1
volume 1 name "mybrick"
list names volume

# This will make all coordinates positive. Not necessary
# but can be convenient. Cubit uses symmetrical coordinates by default.
move vertex 4 location 0 0 0 nomerge

# Cut the volume in half to refine the pn junction region
webcut volume 1 with plane xplane offset 0.5 noimprint merge
merge all

# Define sidesets for Dirichlet BC
sideset 1 curve 3
sideset 1 name "anode"
sideset 2 curve 1
sideset 2 name "cathode"

# Define blocks for a single material
block 1 surface 15 8
block 1 name "Silicon"
block 1 element type quad4

# Define meshing size on the long side
curve 26 18 interval 50
curve 26 18 scheme equal

# Define meshing size on the short side
curve 3 interval 10
curve 3 scheme equal

# Actually mesh the geometry using the default meshing engine (Map)
surface 15 8 scheme map
mesh surface all

# Refine mesh at the junction
# refine curve 15 numsplit 2 bias 1 depth 5 smooth

# Merge all meshing elements
merge all

# the structure size is assumed in [um] in Cubit, but
# choose centimeters as the length in Panzer. 

# Output the Exodus II mesh
export mesh "pndiode.exo" dimension 2 block all overwrite



