#     {tox = 0.05}
#     {clen = 7.0}
#     {wsub = 8}
#     {tsub = 2}
#     {xj = 0.2}

# vertices for Silicon region
create vertex {-wsub/2.0} {-tsub} 0 
create vertex {-clen/2.0} {-tsub} 0
create vertex {clen/2.0} {-tsub} 0
create vertex {wsub/2.0} {-tsub} 0

create vertex {wsub/2.0} {-xj} 0
create vertex {clen/2.0} {-xj} 0
create vertex {-clen/2.0} {-xj} 0
create vertex {-wsub/2.0} {-xj} 0

create vertex {wsub/2.0} 0 0
create vertex {clen/2.0} 0 0
create vertex {-clen/2.0} 0 0
create vertex {-wsub/2.0} 0 0

# vertices for SiO2 region
create vertex {clen/2.0} {tox} 0
create vertex {-clen/2.0} {tox} 0

# bottom-left substrate
create surface vertex 1 2 7 8

# bottom-center substrate
create surface vertex 2 3 6 7

# bottom-right substrate
create surface vertex 3 4 5 6

# drain region
create surface vertex 6 5 9 10

# channel region
create surface vertex 7 6 10 11

# source region
create surface vertex 8 7 11 12

# SiO2 region
create surface vertex 11 10 13 14

# merge all overlapped curves
merge all

# define nodesets for BCs
sideset 1 curve 27
sideset 1 name "gate"
sideset 2 curve 1 5 9
sideset 2 name "substrate"
sideset 3 curve 23
sideset 3 name "source"
sideset 4 curve 15
sideset 4 name "drain"
sideset 5 curve 19
sideset 5 name "sheet"

# define blocks (each block represents one material)
block 1 surface 1 2 3 4 5 6
block 1 name "silicon"
block 1 element type quad4

block 2 surface 7
block 2 name "sio2"
block 2 element type quad4

# define meshing size at the y-direction
# very fine mesh at the Silicon/Oxide interface
curve 14 16 20 24 scheme dualbias fine size 1e-3 factor 1.25
curve 26 scheme bias fine size 1e-3 coarse size {tox/5.0} start vertex 10
curve 28 scheme bias fine size 1e-3 coarse size {tox/5.0} start vertex 11

# fine mesh at the source/subsrate and drain/substrate junctions along the y-direction
curve 10 interval 50
curve 10 scheme bias first_delta 5e-3 start vertex 5

curve 6 interval 50
curve 6 scheme bias first_delta 5e-3 start vertex 6

curve 2 interval 50
curve 2 scheme bias first_delta 5e-3 start vertex 7

curve 4 interval 50
curve 4 scheme bias first_delta 5e-3 start vertex 8

# along the x-direction
curve 23 interval 30
curve 23 scheme bias first_delta 5e-3 start vertex 11

curve 3 interval 30
curve 3 scheme bias first_delta 5e-3 start vertex 7

curve 1 interval 30
curve 1 scheme bias first_delta 5e-3 start vertex 2

curve 27 19 7 5 scheme dualbias fine size 5e-3 coarse size 0.2

curve 15 interval 30
curve 15 scheme bias first_delta 5e-3 start vertex 10

curve 11 interval 30
curve 11 scheme bias first_delta 5e-3 start vertex 6

curve 9 interval 30
curve 9 scheme bias first_delta 5e-3 start vertex 3

# actually mesh the geometry using the default meshing engine (Map)
mesh surface all

# merge all overlapped elements
merge all

# dimension size is assumed in [um]
# output the Exodus II mesh
export mesh "nmosfet.exo" dimension 2 block all overwrite


