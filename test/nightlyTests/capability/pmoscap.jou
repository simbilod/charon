#     {wsub = 20}
#     {tsub = 10}
#     {tox = 1}

# vertices for Silicon substrate
create vertex {-wsub/2.0} 0 0 
create vertex {wsub/2.0} 0 0
create vertex {wsub/2.0} {tsub} 0
create vertex {-wsub/2.0} {tsub} 0

# vertices for SiO2 region
create vertex {-wsub/2.0} {-tox} 0
create vertex {wsub/2.0} {-tox} 0

# create the Silicon region
create surface vertex 1 2 3 4

# create the SiO2 region
create surface vertex 1 5 6 2

# merge all overlapped curves
merge all

# define nodesets for BCs
sideset 1 curve 3
sideset 1 name "substrate"
color sideset 1 blue

sideset 2 curve 6
sideset 2 name "gate"
color sideset 1 red

# define blocks (each block represents one material)
block 1 surface 1
block 1 name "psilicon"
block 1 element type quad4

block 2 surface 2
block 2 name "sio2"
block 2 element type quad4

# define meshing size at the x-direction 
curve 3 interval 40
curve 3 scheme equal

# define meshing size at the y-direction
curve 4 interval 20
curve 4 scheme equal

curve 5 interval 2
curve 5 scheme equal

# actually mesh the geometry using the default meshing engine (Map)
surface 1 2 scheme map
mesh surface all

# refine mesh in Silicon near the Silicon/SiO2 interface
# one refinement generates 1800 elements
refine curve 1 numsplit 1 bias 1.5 depth 5 smooth

# merge all overlapped elements
merge all

# dimension size is assumed in [um]
# output the Exodus II mesh
export mesh "pmoscap.exo" dimension 2 block all overwrite


